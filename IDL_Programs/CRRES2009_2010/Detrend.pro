C----------------------------------------------------------------------------
	SUBROUTINE DETREND(XX,YY,NN,MMF)
C-----------------------------------------------------------------------------
	REAL XX(NN),YY(NN),CC(10)
	CALL HAVAFIT(XX,YY,NN,MMF,CC,BBETA)
	DO 10 I=1,NN
	  Y=0.0
	  DO 20 J=0,MMF
20	    Y=Y+CC(J+1)*XX(I)**J
	  YY(I)=YY(I)-Y
10	CONTINUE

C
C	DETRENDED INFORMATION HERE
C

	DO 40 K=1,NN
	  WRITE(2,30)XX(K),YY(K)
30	  FORMAT(2f15.6)
40	CONTINUE

	RETURN
	END

C----------------------------------------------------------------------------
	SUBROUTINE HAVAFIT(X,Y,N,MF,C,BETA)
C-----------------------------------------------------------------------------
	DIMENSION X(N),Y(N),C(10),A(10,11),XN(3500)
C COMPUTE MATRIX OF COEFFICIENTS AND R.H.S. FOR MF DEGREE
C HOWEVER, FIRST CHECK TO SEE IF MAX DEGREE REQUESTED IS TOO
C LARGE-IT CANNOT EXCEED N-1. IF IT DOES, REDUCE TO EQUAL N-1
C AND PRINT MESSAGE.
	print*,'1'
	IF (MF.LE.(N-1)) GO TO 5
	MF=N-1
	print*,'2'
	WRITE(6,200) MF
200	FORMAT(' DEGREE TOO LARGE, REDUCED TO ',I3)
5	MFP1=MF+1
	MFP2=MF+2
C PUT ONES INTO NEW ARRAY. WILL HOLD POWERS OF X VALUES AS WE PROCEED
	print*,'3'
	DO 10 I=1,N
	  XN(I)=1.0
10	CONTINUE
	DO 30 I=1,MFP1
	  A(I,1)=0.0
	  A(I,MFP2)=0.0
	  DO 20 J=1,N
	    A(I,1)=A(I,1)+XN(J)
	    A(I,MFP2)=A(I,MFP2)+Y(J)*XN(J)
	    XN(J)=XN(J)*X(J)
20	  CONTINUE
30	CONTINUE
	print*,'4'
C COMPUTE THE LAST ROW OF A. I MOVES ACROSS THE COLUMNS, J SUMS OVER N
	DO 50 I=2,MFP1
	  A(MFP1,I)=0.0
	  DO 40 J=1,N
	    A(MFP1,I)=A(MFP1,I)+XN(J)
	    XN(J)=XN(J)*X(J)
40	  CONTINUE
50	CONTINUE
C FILL IN REST OF MATRIX, I MOVES DOWN ROWS, J MOVES ACROSS COLUMNS
	DO 70 J=2,MFP1
	  DO 60 I=1,MF
	    A(I,J)=A(I+1,J-1)
60	  CONTINUE
70	CONTINUE
C GET THE LU DECOMPOSITION OF A
	print*,'5'
	CALL LUDCMQ(A,MFP1,10)
C RESET THE R.H.S. INTO C.
	DO 90 J=1,MFP1
	  C(J)=A(J,MFP2)
90	CONTINUE
	CALL SOLNQ(A,C,MFP1,10)

C	OUTPUT THE COEFFICIENT OF THE POLYNOMIAL BEST FIT
C
	WRITE(*,110)MFP1-1
110	FORMAT(' FOR DEGREE ',I3,' THE POLYNOMIAL HAS COEFFICIENT ')

	DO 120 JSUM=1,MFP1
	  WRITE(*,115) C(JSUM)
115	  FORMAT(F12.5)
120	CONTINUE

C COMPUTE THE VALUE OF BETA = SUM OF DEV. SQUARED/(N-M-1)
	BETA=0.0
	DO 94 IPT=1,N
	  SUM=0.0
	  DO 93 ICOEF=2,MFP1
	    JCOEF=MFP1-ICOEF+2
	    SUM=(SUM+C(JCOEF))*X(IPT)
93	  CONTINUE
	  SUM=SUM+C(1)
	  BETA=BETA+(Y(IPT)-SUM)**2
94	CONTINUE
	BETA=BETA/(N-MFP1)
	RETURN
	END

C=============================================================================
C	THIS SUBROUTINE IS A SATELLITE OF HAVAFIT.
C
C FORM THE LU EQUIVALENT OF THE SQUARE COEFFICIENT MATRIX A. THE LU, IN
C COMPACT FORM, IS RETURNED IN THE A MATRIX SPACE. THE UPPER TRIANGULAR
C MATRIX U HAS ONES IN ITS DIAGONAL-NOT INCLUDED IN RESULT.
C-----------------------------------------------------------------------------
	SUBROUTINE LUDCMQ(A,N,NDIM)
C-----------------------------------------------------------------------------
	DIMENSION A(NDIM,NDIM)
	DO 30 I=1,N
	DO 30 J=2,N
	SUM=0.0
	IF (J.GT.I) GOTO 15
	JM1=J-1
	DO 10 K=1,JM1
10	SUM=SUM+A(I,K)*A(K,J)
	A(I,J)=A(I,J)-SUM
	GO TO 30
15	IM1=I-1
	IF (IM1.EQ.0) GO TO 25
	DO 20 K=1,IM1
20	SUM=SUM+A(I,K)*A(K,J)
C TEST FOR SMALL VALUE ON DIAGONAL
25	IF ( ABS(A(I,I)) .LT. 1.0E-10) GO TO 99
	A(I,J)=(A(I,J)-SUM)/A(I,I)
30	CONTINUE
	RETURN
99	WRITE(6,100) I
100	FORMAT(' SMALL VALUE FOUND FOR DIVISOR IN ROW ', I3)
	RETURN
	END

